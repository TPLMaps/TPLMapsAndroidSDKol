apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.FarazKhalidZaki'

android {
    compileSdkVersion 25

    /*
     * Ubuntu can't run the aapt on 64 bit before installing this packages
     *    sudo apt-get install lib32stdc++6
     *    sudo apt-get install lib32z1
     */

    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
    }
    sourceSets.main {
        jniLibs.srcDirs = ['libs']
    }
}
// Add gdb server to apk
afterEvaluate {
    Sync packageTask = project.getTasks().findByName("packageReleaseJniLibs")
    if (packageTask) {
        packageTask.include(['**/gdbserver', '**/gdb.setup'])
    }

    packageTask = project.getTasks().findByName("packageDebugJniLibs")
    if (packageTask) {
        packageTask.include(['**/gdbserver', '**/gdb.setup'])
    }
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'xmlpull:xmlpull:1.1.3.1'
    compile 'com.android.support:support-annotations:25.0.1'
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
